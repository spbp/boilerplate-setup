// Import plugins
buildscript {
	repositories {
		jcenter()
	}
	dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'license'

ext.projectName = 'boilerplate-setup'
ext.packaging = 'jar'
ext.author = 'SPBP'
ext.authorUrl = 'https://github.com/spbp/'
ext.inceptionYear = '2015'

version = '0.1.0-SNAPSHOT'

group = 'com.github.spbp.boilerplate'
archivesBaseName = project.name.toLowerCase()

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'net.sf.qdwizard:qdwizard:5.0.0'
    compile 'com.miglayout:miglayout-swing:5.0'
}

shadowJar {
    dependencies {
        include dependency('net.sf.qdwizard:qdwizard')
        include dependency('com.miglayout:miglayout-core')
        include dependency('com.miglayout:miglayout-swing')
    }
}

// Run shadowJar on build
assemble.dependsOn shadowJar

	
license {
    ext.name = rootProject.projectName
    ext.author = rootProject.author
    ext.url = rootProject.authorUrl
    ext.year = rootProject.inceptionYear
    exclude "**/*.info"
    exclude "**/*.html"
    exclude "assets/**"
    header new File(rootProject.getProjectDir(), "HEADER.txt")
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

processResources {
    from new File(rootProject.getProjectDir(), "LICENSE.txt")
}

jar {
    manifest {
        attributes( 
        "Main-Class": "com.github.spbp.setup.BoilerplateSetupWizard", 
        "Implementation-Title": name, 
        "Implementation-Version": version)
    }
}